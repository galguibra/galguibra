=
LET(
    lookup_participant, @A:A,

    blank_above, ISBLANK( OFFSET( lookup_participant, -1, 0 ) ),

    IF(
        ISBLANK( lookup_participant ),

        IF(
            blank_above,

            MAKEARRAY( 1, 9, LAMBDA( row, col, "" ) ),

            MAKEARRAY( 1, 9, LAMBDA( row, col, "Input Participant #" ) )
        ),

        LET(
           participant_list, '[Couple and Participant Data.xlsm]Participant Communications'!$A$3:$A$218,

           name, '[Couple and Participant Data.xlsm]Participant Communications'!$B$3:$D$218,

           language, '[Couple and Participant Data.xlsm]Participant Communications'!$E$3:$E$218,

           phone, '[Couple and Participant Data.xlsm]Participant Communications'!$F$3:$F$218,

           email, '[Couple and Participant Data.xlsm]Participant Communications'!$G$3:$G$218,

           communications_flag, '[Couple and Participant Data.xlsm]Participant Communications'!$H$3:$H$218,

           interview_status, '[Couple and Participant Data.xlsm]Participant Milestones'!$B$3:$B$218,

           awaiting_eligibility, 'Awaiting Eligibility Tracker'!$A$3#,

           LET(
               participants_awaiting, TAKE( awaiting_eligibility, , 1 ),

               eligibility_dates, TAKE( awaiting_eligibility, , -1 ),

               preferred_language, IF( language = 1, "Spanish", "English" ),

               communications_status, IFS(
                   communications_flag = -6, 'Overview Stats'!$A$10,

                   communications_flag = -5, 'Overview Stats'!$A$11,

                   communications_flag = -4, 'Overview Stats'!$A$12,

                   communications_flag = -3, 'Overview Stats'!$A$13,

                   communications_flag = -2, 'Overview Stats'!$A$14,

                   communications_flag = -1, 'Overview Stats'!$A$4,

                   communications_flag = 0, 'Overview Stats'!$A$5,

                   communications_flag = 1, 'Overview Stats'!$A$6,

                   communications_flag = 2, 'Overview Stats'!$A$7,

                   communications_flag = 3, 'Overview Stats'!$A$8
               ),

               can_contact, ( communications_flag >= 0 ) * ( communications_flag <= 2 ),

               engagement_status, IFS(
                   ( ( interview_status = 0 ) * can_contact ) + ( communications_flag = -1 ),
                   'Overview Stats'!$D$4,

                   ( interview_status = -1 ) * can_contact,
                   'Overview Stats'!$D$5,

                   ( interview_status = -2 ) * can_contact,
                   'Overview Stats'!$D$6,

                   interview_status >= 1, 'Overview Stats'!$D$8,

                   ( communications_flag <= -2 ) * ( interview_status <= 0 ),
                   'Overview Stats'!$D$9,

                   communications_flag = 3,
                   'Overview Stats'!$D$10
               ),

               eligibility_status, IF(
                   engagement_status = "Awaiting Eligibility",

                   XLOOKUP(
                       lookup_participant,

                       participants_awaiting,

                       eligibility_dates,

                       "Invalid Participant #"
                   ),

                   "Not Awaiting Eligibility"
               ),

               XLOOKUP(
                   lookup_participant,

                   participant_list,

                   HSTACK(
                       name,

                       preferred_language,

                       IF( ISBLANK( phone ), "Unavailable", phone ),

                       email,

                       communications_status,

                       engagement_status,

                       eligibility_status
                   ),

                   MAKEARRAY( 1, 9, LAMBDA( row, col, "Invalid Participant #" ) )
                )
            )
        )
    )
)
